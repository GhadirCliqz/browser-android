apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.cliqz.browser.helper"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 4
        versionName "1.0"
        setProperty("archivesBaseName", "testsHelper_v$versionCode")
    }

    signingConfigs {
        common {
            storeFile file('../debug.keystore')
            storePassword 'android'
            keyAlias 'debug'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.common
        }
        debug {
            signingConfig signingConfigs.common
        }
    }
}

android.applicationVariants.all { variant ->
    def zipTask = project.tasks.create("zip${variant.buildType.name}", Zip)
    zipTask
            .from(variant.outputs[0].outputFile)
            .into("${buildDir}/testsHelper_v${variant.versionCode}.zip")
            .dependsOn(variant.assemble)
    zipTask.eachFile { fcd ->
        fcd.setPath(fcd.getSourceName())
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.jakewharton:butterknife:7.0.1'
}
