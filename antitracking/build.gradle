apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def copy_adblock_list(File outputDir, String path, String target, String name) {
    File parent = project.buildscript.sourceFile.parentFile
    File adbFile = new File(parent, path + name)
    File copyTo = new File(outputDir, 'v8/modules/adblocker/' + target)
    copyTo.mkdirs()
    new File(copyTo, name).withWriter { writer ->
        adbFile.eachLine { line ->
            writer.println(line)
        }
    }
}

def copy_adblock_lists(File outputDir) {
    String adbAssetPrefix = 'adblock_assets/'
    copy_adblock_list(outputDir, adbAssetPrefix, 'assets/ublock/', 'filter-lists.json')
    copy_adblock_list(outputDir, adbAssetPrefix, 'assets/ublock/', 'filters.txt')
    copy_adblock_list(outputDir, adbAssetPrefix, 'assets/ublock/', 'unbreak.txt')
    copy_adblock_list(outputDir, adbAssetPrefix, '', 'checksums')
    copy_adblock_list(outputDir, adbAssetPrefix, 'raw.githubusercontent.com/reek/anti-adblock-killer/master/', 'anti-adblock-killer-filters.txt')
    copy_adblock_list(outputDir, adbAssetPrefix, 'easylist-downloads.adblockplus.org/', 'antiadblockfilters.txt')
    copy_adblock_list(outputDir, adbAssetPrefix, 'assets/thirdparties/easylist-downloads.adblockplus.org/', 'easylist.txt')
    copy_adblock_list(outputDir, adbAssetPrefix, 'pgl.yoyo.org/as/', 'serverlist')
}

afterEvaluate { project ->
    project.tasks.each { tazk ->
        if (tazk.name =~ /merge.*Assets/ && !tazk.name.endsWith('TestAssets')) {
            tazk << {
                copy_adblock_lists(tazk.outputDir)
            }
        }
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.0.1'
    // Embedded V8 engine
    compile "com.eclipsesource.j2v8:j2v8:4.1.0@aar"
    compile project(':utils')
}
